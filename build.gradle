plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '4.0.4' // AsciiDoc 처리를 위한 플러그인
}

group = 'joomidang'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

dependencies {
    // Web
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Spring Data JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Spring Security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // OAuth2 Client
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    //RabbitMQ
    implementation 'org.springframework.boot:spring-boot-starter-amqp'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Spring REST Docs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // H2
    runtimeOnly 'com.h2database:h2'

    // MySql
    runtimeOnly 'com.mysql:mysql-connector-j'

    //AWS S3
    implementation 'software.amazon.awssdk:s3:2.30.16'
    implementation 'software.amazon.awssdk:auth'
    implementation 'software.amazon.awssdk:regions'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'

    // Thumbnailator - Image processing
    implementation 'net.coobird:thumbnailator:0.4.20'

    // Elasticsearch
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
}

tasks.named('test') {
    useJUnitPlatform()
}

ext {
    activeProfile = project.hasProperty('spring.profiles.active') ? project.property('spring.profiles.active') : 'default'
    snippetsDir = file('build/generated-snippets')
}

tasks.register('restDocsTest', Test) {
    description = "Run only REST Docs related tests"
    group = "verification"

    outputs.dir snippetsDir

    useJUnitPlatform()
    filter {
        includeTestsMatching "*.docs.*"
    }

    onlyIf {
        activeProfile != 'prod'
    }
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'

    sources { // 특정 파일만 html로 만든다.
        include("**/index.adoc", "**/enum/*.adoc")
    }
    baseDirFollowsSourceFile() // 다른 adoc 파일을 include 할 때 경로를 baseDir로 맞춘다.
    dependsOn restDocsTest

    onlyIf {
        activeProfile != 'prod'
    }
}

bootJar {
    archiveFileName = "${rootProject.name}.jar"

    if (activeProfile != 'prod') {
        dependsOn asciidoctor
        from("${asciidoctor.outputDir}") {
            into 'static/docs'
        }
    }
}
